buildscript {
    repositories {
        maven { url 'https://maven.aliyun.com/repository/public'}
    }
    dependencies {
        classpath 'com.google.protobuf:protobuf-gradle-plugin:0.8.8'
    }
}

plugins {
    id 'java'
    id 'signing'
}

def appName = "pdf-client-wrapper"
group 'io.gitee.passer-team'
version '0.0.1'

def grpcVersion = '1.35.0'

apply plugin: 'idea'
apply plugin: 'com.google.protobuf'

// >>> Start of maven publish
// 参考 https://central.sonatype.org/publish/publish-gradle/#deployment
apply plugin: 'maven'  // 发布到maven仓库需要，
apply plugin: 'signing'  // 发布到maven仓库需要
task javadocJar(type: Jar) {
    classifier = 'javadoc'
    from javadoc
}
task sourcesJar(type: Jar) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

artifacts {
    archives javadocJar, sourcesJar
}

signing {
    sign configurations.archives
}

archivesBaseName = appName

uploadArchives {
    repositories {
        mavenDeployer {
            beforeDeployment { MavenDeployment deployment -> signing.signPom(deployment) }

            repository(url: "https://s01.oss.sonatype.org/service/local/staging/deploy/maven2/") {
                authentication(userName: ossrhUsername, password: ossrhPassword)
            }

            snapshotRepository(url: "https://s01.oss.sonatype.org/content/repositories/snapshots/") {
                authentication(userName: ossrhUsername, password: ossrhPassword)
            }

            pom.project {
                name appName
                packaging 'jar'
                // optionally artifactId can be defined here
                description 'A pdf client wrapper, gRPC based application.'
                url 'https://gitee.com/passer-team/pdf-client-wrapper'

                scm {
                    connection 'scm:git:https://gitee.com/passer-team/pdf-client-wrapper.git'
                    developerConnection 'scm:git:https://gitee.com/passer-team/pdf-client-wrapper.git'
                    url 'https://gitee.com/passer-team/pdf-client-wrapper'
                }

                licenses {
                    license {
                        name 'The MIT License'
                        url 'https://opensource.org/licenses/MIT'
                    }
                }

                developers {
                    developer {
                        id 'daryl6'
                        name 'Daryl Xu'
                        email 'ziqiang_xu@qq.com'
                    }
                }
            }
        }
    }
}

// <<< End of maven publish

repositories {
    mavenCentral()
}

sourceCompatibility = 1.8
targetCompatibility = 1.8

dependencies {
    // https://mvnrepository.com/artifact/javax.annotation/javax.annotation-api
//    implementation group: 'javax.annotation', name: 'javax.annotation-api', version: '1.3.2'

    // gRPC
    compile "io.grpc:grpc-netty-shaded:${grpcVersion}"
    compile "io.grpc:grpc-protobuf:${grpcVersion}"
    compile "io.grpc:grpc-stub:${grpcVersion}"
    implementation("javax.annotation:javax.annotation-api:1.3.2")

    testCompile group: 'junit', name: 'junit', version: '4.12'
}

protobuf {
    // 需要安装插件 com.google.protobuf
    // 并将proto文件放在main目录下
    protoc {
        artifact = "com.google.protobuf:protoc:3.7.1"
    }

    plugins {
        grpc {
            artifact = "io.grpc:protoc-gen-grpc-java:${grpcVersion}"
        }
    }

    generateProtoTasks {
        all()*.plugins {
            grpc {}
        }
    }
}
